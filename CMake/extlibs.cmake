set(GLEW_ORIGIN "INTERNAL" CACHE STRING "Origin of GLEW library. Possible values are: INTERNAL (default), SYSTEM")
if(GLEW_ORIGIN STREQUAL "INTERNAL")
    execute_process(COMMAND ${CMAKE_CURRENT_SOURCE_DIR}/External/extract-glew.sh ${CMAKE_CURRENT_BINARY_DIR} RESULT_VARIABLE GLEW_EXTRACT_RET)
    if(NOT GLEW_EXTRACT_RET STREQUAL "0")
        message(FATAL_ERROR "extract-glew.sh failed")
    endif()
    set(BUILD_UTILS OFF CACHE BOOL "GLEW utilities" FORCE)
    add_subdirectory(${CMAKE_CURRENT_BINARY_DIR}/glew-2.1.0/build/cmake glew-build)
    add_library(GLEW INTERFACE)
    target_link_libraries(GLEW INTERFACE glew_s)
    target_include_directories(GLEW INTERFACE ${CMAKE_CURRENT_BINARY_DIR}/glew-2.1.0/include)
    target_compile_definitions(GLEW INTERFACE -DGLEW_NO_GLU)
elseif(GLEW_ORIGIN STREQUAL "SYSTEM")
    find_library(_GLEW_PATH GLEW)
    if(NOT _GLEW_PATH)
        message(FATAL_ERROR "GLEW library not found (-DGLEW_ORIGIN=SYSTEM)")
    endif()
    add_definitions(-DGLEW_NO_GLU)
else()
    message(FATAL_ERROR "GLEW_ORIGIN not understood: ${GLEW_ORIGIN}")
endif()

set(GLFW_ORIGIN "INTERNAL" CACHE STRING "Origin of GLFW library. Possible values are: INTERNAL (default), SYSTEM")
if(GLFW_ORIGIN STREQUAL "INTERNAL")
    execute_process(COMMAND ${CMAKE_CURRENT_SOURCE_DIR}/External/extract-glfw.sh ${CMAKE_CURRENT_BINARY_DIR} RESULT_VARIABLE GLFW_EXTRACT_RET)
    if(NOT GLFW_EXTRACT_RET STREQUAL "0")
        message(FATAL_ERROR "extract-glfw.sh failed")
    endif()
    set(GLFW_BUILD_EXAMPLES OFF CACHE BOOL "GLFW examples" FORCE)
    set(GLFW_BUILD_TESTS OFF CACHE BOOL "GLFW tests" FORCE)
    set(GLFW_BUILD_DOCS OFF CACHE BOOL "GLFW docs" FORCE)
    set(GLFW_INSTALL OFF CACHE BOOL "GLFW install" FORCE)
    add_subdirectory(${CMAKE_CURRENT_BINARY_DIR}/glfw-3.2.1 glfw-build)
elseif(GLFW_ORIGIN STREQUAL "SYSTEM")
    find_library(_GLFW_PATH glfw)
    if(NOT _GLFW_PATH)
        message(FATAL_ERROR "GLFW library not found (-DGLFW_ORIGIN=SYSTEM)")
    endif()
else()
    message(FATAL_ERROR "GLFW_ORIGIN not understood: ${GLFW_ORIGIN}")
endif()
